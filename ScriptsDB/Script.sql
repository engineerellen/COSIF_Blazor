
IF DB_ID('COSIF') IS NULL
    CREATE DATABASE COSIF;
GO
USE COSIF;
GO

IF OBJECT_ID('dbo.PRODUTO') IS NOT NULL DROP TABLE dbo.PRODUTO;
CREATE TABLE dbo.PRODUTO (
    COD_PRODUTO CHAR(4) NOT NULL PRIMARY KEY,
    DES_PRODUTO VARCHAR(30) NOT NULL,
    STA_STATUS CHAR(1) NOT NULL
);
GO

IF OBJECT_ID('dbo.PRODUTO_COSIF') IS NOT NULL DROP TABLE dbo.PRODUTO_COSIF;
CREATE TABLE dbo.PRODUTO_COSIF (
    COD_COSIF VARCHAR(11) NOT NULL PRIMARY KEY,
    COD_PRODUTO CHAR(4) NOT NULL,
    COD_CLASSIFICACAO CHAR(6) NULL,
    STA_STATUS CHAR(1) NOT NULL,
    CONSTRAINT FK_PRODUTO_COSIF_PRODUTO FOREIGN KEY (COD_PRODUTO) REFERENCES dbo.PRODUTO(COD_PRODUTO)
);
GO

IF OBJECT_ID('dbo.MOVIMENTO_MANUAL') IS NOT NULL DROP TABLE dbo.MOVIMENTO_MANUAL;
CREATE TABLE dbo.MOVIMENTO_MANUAL (
    DAT_MES TINYINT NOT NULL,
    DAT_ANO SMALLINT NOT NULL,
    NUM_LANCAMENTO BIGINT NOT NULL,
    COD_PRODUTO CHAR(4) NOT NULL,
    COD_COSIF VARCHAR(11) NOT NULL,
    VAL_VALOR DECIMAL(18,2) NOT NULL,
    DES_DESCRICAO VARCHAR(50) NOT NULL,
    DAT_MOVIMENTO SMALLDATETIME NOT NULL,
    COD_USUARIO VARCHAR(15) NOT NULL,
    CONSTRAINT PK_MOVIMENTO_MANUAL PRIMARY KEY (DAT_MES, DAT_ANO, NUM_LANCAMENTO),
    CONSTRAINT FK_MOVIMENTO_PRODUTO FOREIGN KEY (COD_PRODUTO) REFERENCES dbo.PRODUTO(COD_PRODUTO),
    CONSTRAINT FK_MOVIMENTO_COSIF FOREIGN KEY (COD_COSIF) REFERENCES dbo.PRODUTO_COSIF(COD_COSIF)
);
GO

CREATE OR ALTER PROCEDURE dbo.SP_GetMovimentos
AS
BEGIN
    SET NOCOUNT ON;
    SELECT
        m.DAT_MES,
        m.DAT_ANO,
        m.NUM_LANCAMENTO,
        m.COD_PRODUTO,
        p.DES_PRODUTO,
        m.DES_DESCRICAO,
        m.VAL_VALOR,
        m.DAT_MOVIMENTO,
        m.COD_USUARIO,
        m.COD_COSIF
    FROM dbo.MOVIMENTO_MANUAL m
    LEFT JOIN dbo.PRODUTO p ON p.COD_PRODUTO = m.COD_PRODUTO
    ORDER BY m.DAT_ANO, m.DAT_MES, m.NUM_LANCAMENTO;
END
GO

CREATE OR ALTER PROCEDURE dbo.SP_GetNextLancamento
    @DAT_MES TINYINT,
    @DAT_ANO SMALLINT,
    @NEXT_LANCAMENTO BIGINT OUTPUT
AS
BEGIN
    SET NOCOUNT ON;
    SELECT @NEXT_LANCAMENTO = ISNULL(MAX(NUM_LANCAMENTO), 0) + 1
    FROM dbo.MOVIMENTO_MANUAL
    WHERE DAT_MES = @DAT_MES AND DAT_ANO = @DAT_ANO;
END
GO